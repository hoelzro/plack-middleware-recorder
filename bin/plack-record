#!/usr/bin/env perl

use strict;
use warnings;

use Plack::Builder;
use Plack::Runner;
use Plack::Middleware::Recorder;
use Plack::Util;

my $runner = Plack::Runner->new;
$runner->parse_options(@ARGV);

if($runner->{'help'}) {
    exec 'plackup', '--help';
}

my $app = $runner->{'app'} || $runner->{'argv'}[0] || 'app.psgi';
$app    = Plack::Util::load_psgi $app;

$runner->run(builder {
    enable 'Recorder', output => \*STDOUT;
    $app;
});

__END__

# PODNAME: plack-record

# ABSTRACT: Utility script for recording PSGI app requests

=head1 SYNOPSIS

  plack-record > requests.out

=head1 DESCRIPTION

This script records the requests that go to your PSGI application and writes
their serialized forms to standard output.

=head1 OPTIONS

Supports all options that L<plackup> does.

=head1 SEE ALSO

L<plackup>, L<Plack::Middleware::Recorder>, L<plack-replay>

=cut
